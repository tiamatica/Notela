:Class TracerProvider: ITracerProvider
    :Include Tools

    :Field Private Instance _cfgDebug←0
    :Field Private Instance _cfgBatchSize
    :Field Private Instance _cfgExporters
    :Field Private Instance _cfgLoggers
    :Field Private Instance _cfgLogLevel
    :Field Private Instance _cfgServiceName
    :Field Private Instance _cfgServiceVersion

    :Field Private Instance _httpClt
    :Field Private Instance _threads
    :Field Private Instance _traces
    :Field Private Instance _resourceSpan
    :Field Private Instance _isShuttingDown

    :Field Private Shared PS←##

    ⍝ Tracer Provider initialization
    ∇ make cfg;resource
      :Access Public
      :Implements Constructor
      ⎕RL←⍬ 1
      _cfgDebug←cfg.Debug
      _cfgBatchSize←cfg.BatchSize
      _cfgExporters←cfg.Exporters
      _cfgLoggers←cfg.Loggers
      _cfgLogLevel←cfg.LogLevel
      _cfgServiceName←cfg.ServiceName
      _cfgServiceVersion←cfg.ServiceVersion
     
      :Trap _cfgDebug↓0
          _threads←⍬
          _isShuttingDown←0
     
          _traces←⎕NS''
          _resourceSpan←_traces.⎕NS''
          _traces.resourceSpans←,_resourceSpan
          resource←_resourceSpan.(resource←⎕NS'')
          resource.attributes←toKeyValue/2 2⍴'service.name'cfg.ServiceName'service.version'cfg.ServiceVersion
          _resourceSpan.scopeSpans←⍬
     
          _httpClt←⎕NEW PS.HttpCommand
          _httpClt.Command←'post'
          _httpClt.URL←cfg.OtelHttpUrlTraces
          _httpClt.ContentType←'application/json'
      :EndTrap
      ⍝
    ∇

    ∇ r←GetTracer args;ind;name;version;scopeName;scopeVersion;attrs;scopeSpan
      :Implements Method ITracerProvider.GetTracer
      ⍝ args = ScopeName [ScopeVersion] [attributes]
      :Trap _cfgDebug↓0
          :If 0=≢args
              scopeName scopeVersion←_cfgServiceName _cfgServiceVersion
              attrs←0 2⍴⊂''
          :Else
              args←,⊆args
              args←args,(1=≢args)/⊂''
              scopeName scopeVersion←2↑args
              attrs←checkAttributes toKeyValueMatrix 2↓args
          :EndIf
          scopeSpan←_resourceSpan.⎕NS''
          scopeSpan.(scope←⎕NS'')
          scopeSpan.scope.(name version)←scopeName scopeVersion
          scopeSpan.scope.attributes←toKeyValue/attrs
          scopeSpan.spans←⍬
          _resourceSpan.scopeSpans,←scopeSpan
          r←⎕NEW PS.Tracer(,scopeSpan)
      :Else
          logError
          r←⎕NEW PS.TracerNOP
      :EndTrap
    ∇

    ∇ r←GetContext;last
      :Implements Method ITracerProvider.GetContext
     ⍝ Returns Context
      r←⍬
      :Trap _cfgDebug↓0
          last←PS.GetActiveSpan
          :If 1=≢last
              r←last.GetHttpContext
          :EndIf
      :Else
          logError
      :EndTrap
    ∇

    ∇ r←GetActiveSpan
      :Implements Method ITracerProvider.GetActiveSpan
     ⍝ Gets the span from the current context, if one exists.
      r←⍬
      :Trap _cfgDebug↓0
          r←PS.GetActiveSpan
      :Else
          logError
      :EndTrap
    ∇

    ∇ span←{context}StartSpan args;parent;ind;traceId;parentId;spanId;name;create;scopeSpan
      :Implements Method ITracerProvider.StartSpan
     ⍝ Start span without setting it on context.
     ⍝ Create span and set start time
     ⍝ Create span with name in context.
     ⍝ If no context provided, create span in active span.
     ⍝ If no active span, create new root span.
     ⍝ Does not set the span as active (consider using StartActiveSpan for that).
      :Trap _cfgDebug↓0
          scopeSpan name←2↑args
     
          :If 2=⎕NC'context'
          :AndIf 2=⍴⍴context
              ind←(⎕C context[;1])⍳⊂'traceparent'
          :AndIf ind≤≢context
              (traceId parentId)←¯1↓1↓'-'(≠⊆⊢)⊃context[ind;2]
          :ElseIf ~0∊⍴parent←PS.GetActiveSpan
              traceId←parent.traceId
              parentId←parent.spanId
          :Else
              traceId←'0123456789ABCDEF'[?32⍴16]
              parentId←⊂'null'
          :EndIf
          spanId←'0123456789ABCDEF'[?16⍴16]
     
          span←⎕NEW PS.Span(scopeSpan traceId parentId spanId name _cfgDebug _cfgLogLevel _cfgLoggers)
      :Else
          logError
          span←⎕NEW PS.Span(⍬'' '' '' ''_cfgDebug _cfgLogLevel _cfgLoggers)
      :EndTrap
    ∇

    ∇ span←{context}StartActiveSpan args;currentSpan;row;list
      :Implements Method ITracerProvider.StartActiveSpan
     ⍝ Create span with name in active context.
     ⍝ If no active context, create new root span.
     ⍝ Sets the span as active.
      :Trap _cfgDebug↓0
          :If 0=⎕NC'context'
              span←StartSpan args
          :Else
              span←context StartSpan args
          :EndIf
          PS.SetActiveSpan span
      :Else
          logError
          span←⎕NEW PS.Span(⍬'' '' '' ''_cfgDebug _cfgLogLevel _cfgLoggers)
      :EndTrap
    ∇

    ∇ End span;c;tracespans
      :Access Public Instance
     ⍝ Ended span is sent for further processing
      :Trap _cfgDebug↓0
          ProcessSpans 0
          PS.RemoveActiveSpan span
      :Else
          logError
      :EndTrap
    ∇

    ∇ {r}←ProcessSpans flush;traces;spanBufferLength
     ⍝ Recieve spans for further processing
     ⍝ force = 1 to force flush of buffers
      :If flush∨~_isShuttingDown
      :AndIf 0≠⍴_resourceSpan.scopeSpans
          _threads←(_threads∊⎕TNUMS)/_threads
          spanBufferLength←+/≢¨_resourceSpan.scopeSpans.spans
          :If (flush∧0<spanBufferLength)∨(_cfgBatchSize≤spanBufferLength)
              ⎕TSYNC _threads
              :Hold '_traces'
                  traces←⎕JSON _traces
                  _resourceSpan.scopeSpans.spans←⊂⍬
              :EndHold
              _threads,←ExportSpans&traces
          :EndIf
          :If flush
              ⎕TSYNC _threads
          :EndIf
      :EndIf
      r←0
    ∇

    ∇ ExportSpans traces;scopeSpan;exporter;callspace;fn
     ⍝ Export to OTEL backend or log file
      :Trap _cfgDebug↓0
          :If 0=≢_cfgExporters
              PostHttpJson traces
          :EndIf
          :For exporter :In _cfgExporters
              callspace fn←exporter
              (callspace⍎fn)traces
          :EndFor
      :Else
          logError
      :EndTrap
    ⍝
    ∇

    ∇ PostHttpJson trace;res;ts
      :Hold 'httpPostTrace'
          _httpClt.Params←trace
          res←_httpClt.Run
      :EndHold
      :If ~res.IsOK
          1 logMessage'Failed posting trace'
      :EndIf
    ∇

    ∇ {r}←Shutdown
      :Implements Method ITracerProvider.Shutdown
      r←1
      :Trap _cfgDebug↓0
          _isShuttingDown←1
          r←ProcessSpans 1
      :Else
          logError
      :EndTrap
    ∇

    ∇ {r}←ForceFlush
      :Implements Method ITracerProvider.ForceFlush
      r←1
      :Trap _cfgDebug↓0
          r←ProcessSpans 1
      :Else
          logError
      :EndTrap
    ∇

:EndClass
