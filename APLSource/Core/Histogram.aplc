:Class Histogram: IInstrument
    :Include Tools
⍝
    :Field Private Instance DEBUG←0

    :Field Private Instance bounds
    :Field Private Instance description
    :Field Private Instance name
    :Field Private Instance startTime
    :Field Private Instance values
    :Field Private Instance _metric

    ∇ make2(metric args debug)
      :Access Public
      :Implements Constructor
      DEBUG←debug
      :If {0::1 ⋄ ~(⊃⊃⎕CLASS ⎕THIS).##.IMeterProvider∊⊃⎕CLASS ⍵}⊃⎕RSI
          'Histogram must be created from Meter'⎕SIGNAL 11
      :EndIf
      args←,⊆args
      name description bounds←args,(≢args)↓'' ''(0 5 10 15 10000)
      startTime←timeUnixNano 0
      values←0 2⍴0 ''
     
      _metric←metric
      _metric.name←name
      _metric.unit←,'1'
      _metric.description←description
      _metric.(histogram←⎕NS'')
      _metric.histogram.aggregationTemporality←AGGREGATION_TEMPORALITY_CUMULATIVE
      _metric.histogram.dataPoints←⍬
      ⍝
    ∇

    ∇ Record args;value;attrs
      :Implements Method IInstrument.Record
      :Trap DEBUG↓0
          value←⊃args
          :If 1<≢args
              attrs←{(⊂⍋⍵)⌷⍵}checkAttributes toKeyValueMatrix 1↓args
          :Else
              attrs←0 2⍴⍬
          :EndIf
          values⍪←value attrs
          ##.ProcessMetrics
      :EndTrap
    ∇

    ∇ Clear
      :Implements Method IInstrument.Clear
      :Trap DEBUG↓0
          _metric.histogram.dataPoints←⍬
      :EndTrap
    ∇

    ∇ Collect;dps
      :Implements Method IInstrument.Collect
      :Trap DEBUG↓0
          →(0=≢values)/0
          dps←values[;2]{
              dp←_metric.histogram.⎕NS''
              dp.count←≢⍵
              dp.sum←+/⍵
              dp.bucketCounts←{¯1+≢⍵}⌸(0,⍳≢bounds),bounds⍸⍵
              dp.explicitBounds←bounds
              dp.min←⌊/⍵
              dp.max←⌈/⍵
              dp.attributes←toKeyValue/⊃⍺
              dp
          }⌸values[;1]
          dps.startTimeUnixNano←⊂startTime
          dps.timeUnixNano←⊂startTime←timeUnixNano 0
          _metric.histogram.dataPoints←dps
          values←0⌿values
      :EndTrap
    ∇

    AGGREGATION_TEMPORALITY_UNSPECIFIED ←0
    AGGREGATION_TEMPORALITY_DELTA       ←1
    AGGREGATION_TEMPORALITY_CUMULATIVE  ←2

:EndClass
