:Class MeterProvider: IMeterProvider
    :Include Tools

    :Field Private Instance _cfgDebug←0
    :Field Private Instance _cfgBatchSize
    :Field Private Instance _cfgExporters
    :Field Private Instance _cfgExportInterval
    :Field Private Instance _cfgLoggers
    :Field Private Instance _cfgLogLevel
    :Field Private Instance _cfgServiceName
    :Field Private Instance _cfgServiceVersion

    :Field Private Instance _httpClt
    :Field Private Instance _instruments←⍬
    :Field Private Instance _metrics←⍬
    :Field Private Instance _exportedTime
    :Field Private Instance _bufferLength
    :Field Private Instance _isShuttingDown

    :Field Private Shared PS←##

    ∇ make cfg;ind;name;version;resource
      :Access Public
      :Implements Constructor
    ⍝ cfg = Configuration instance
      _cfgDebug←cfg.Debug
      _cfgBatchSize←cfg.BatchSize
      _cfgExporters←cfg.Exporters
      _cfgLoggers←cfg.Loggers
      _cfgLogLevel←cfg.LogLevel
      _cfgExportInterval←cfg.ExportInterval
      _cfgServiceName←cfg.ServiceName
      _cfgServiceVersion←cfg.ServiceVersion
     
      :Trap _cfgDebug↓0
          _exportedTime←3⊃⎕AI
          _bufferLength←0
          _isShuttingDown←0
     
          _metrics←⎕NS''
          _resourceMetric←_metrics.⎕NS''
          _metrics.resourceMetrics←,_resourceMetric
          resource←_resourceMetric.(resource←⎕NS'')
          resource.attributes←toKeyValue/2 2⍴'service.name'_cfgServiceName'service.version'_cfgServiceVersion
          _resourceMetric.scopeMetrics←⍬
     
          _httpClt←⎕NEW PS.HttpCommand
          _httpClt.Command←'post'
          _httpClt.URL←cfg.OtelHttpUrlMetrics
          _httpClt.ContentType←'application/json'
      :EndTrap
    ∇

    ∇ r←GetMeter args;ind;scopeName;scopeVersion;attrs;scopeMetric
      :Implements Method IMeterProvider.GetMeter
     ⍝ args = ScopeName [ScopeVersion] [attributes]
      :Trap _cfgDebug↓0
          :If 0=≢args
              scopeName scopeVersion←_cfgServiceName _cfgServiceVersion
              attrs←0 2⍴⍬
          :Else
              args←,⊆args
              args←args,(1=≢args)/⊂''
              scopeName scopeVersion←2↑args
              attrs←checkAttributes toKeyValueMatrix 2↓args
          :EndIf
          scopeMetric←_resourceMetric.⎕NS''
          scopeMetric.(scope←⎕NS'')
          scopeMetric.scope.(name version)←scopeName scopeVersion
          scopeMetric.scope.attributes←toKeyValue/attrs
          scopeMetric.metrics←⍬
          _resourceMetric.scopeMetrics,←scopeMetric
          r←⎕NEW PS.Meter(,scopeMetric)
      :Else
          logError
          r←⎕NEW PS.MeterNOP
      :EndTrap
    ∇

    ∇ {r}←Shutdown
      :Implements Method IMeterProvider.Shutdown
      r←1
      :Trap _cfgDebug↓0
          _isShuttingDown←1
          r←ForceFlush
          _instruments←⍬
      :Else
          logError
      :EndTrap
    ∇

    ∇ {r}←ForceFlush
      :Implements Method IMeterProvider.ForceFlush
      r←1
      :Trap _cfgDebug↓0
          ExportMetrics 1
          r←0
      :Else
          logError
      :EndTrap
    ∇

    ∇ instrument←CreateCounter(scopeMetric args);metric
      :Implements Method IMeterProvider.CreateCounter
      :Trap _cfgDebug↓0
          metric←scopeMetric.⎕NS''
          instrument←⎕NEW PS.Counter(metric args _cfgDebug)
          scopeMetric.metrics,←metric
          _instruments,←instrument
      :Else
          logError
          instrument←⎕NEW PS.InstrumentNOP
      :EndTrap
    ∇

    ∇ instrument←CreateGauge(scopeMetric args);metric
      :Implements Method IMeterProvider.CreateGauge
      :Trap _cfgDebug↓0
          metric←scopeMetric.⎕NS''
          instrument←⎕NEW PS.Gauge(metric args _cfgDebug)
          scopeMetric.metrics,←metric
          _instruments,←instrument
      :Else
          logError
          instrument←⎕NEW PS.InstrumentNOP
      :EndTrap
    ∇

    ∇ instrument←CreateHistogram(scopeMetric args);metric
      :Implements Method IMeterProvider.CreateHistogram
      :Trap _cfgDebug↓0
          metric←scopeMetric.⎕NS''
          instrument←⎕NEW PS.Histogram(metric args _cfgDebug)
          scopeMetric.metrics,←metric
          _instruments,←instrument
      :Else
          logError
          instrument←⎕NEW PS.InstrumentNOP
      :EndTrap
    ∇

    ∇ ProcessMetrics
      :Access Public Instance
      :Trap _cfgDebug↓0
          _bufferLength+←1
          ExportMetrics 0
      :Else
          logError
      :EndTrap
    ∇

    ∇ ExportMetrics flush;callspace;exporter;fn;rs;scopeMetric;scopeSpan;metrics
      ⍝ Export metrics for further processing such as batching, sampling and/or enrichment
      ⍝ flush = 1 to force flush buffered meters
      :If 0=≢_instruments
          :Return
      :EndIf
      :If flush∧0<_bufferLength
      :OrIf (~_isShuttingDown)∧(_cfgBatchSize≤_bufferLength)∧(_cfgExportInterval≤(3⊃⎕AI)-_exportedTime)
          _instruments.Collect
          metrics←⎕JSON _metrics
          :If 0=≢_cfgExporters
              PostHttpJson metrics
          :EndIf
          :For exporter :In _cfgExporters
              callspace fn←exporter
              (callspace⍎fn)metrics
          :EndFor
          _instruments.Clear
          _exportedTime←3⊃⎕AI
          _bufferLength←0
      :EndIf
    ⍝
    ∇

    ∇ PostHttpJson metrics;res
      :Hold 'httpPostMeter'
          _httpClt.Params←metrics
          res←_httpClt.Run
      :EndHold
      :If ~res.IsOK
          1 logMessage'Failed posting metric'
      :EndIf
    ∇

:EndClass
