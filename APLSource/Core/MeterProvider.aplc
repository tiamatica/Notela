:Class MeterProvider: IMeterProvider
    :Include Tools

    :Field Private Instance _cfg←⍬
    :Field Private Instance _httpClt
    :Field Private Instance _instruments←⍬
    :Field Private Instance _metrics←⍬
    :Field Private Instance _exportedTime
    :Field Private Instance _bufferLength
    :Field Private Instance _isShuttingDown

    ∇ make cfg;ind;name;version;resource
      :Access Public
      :Implements Constructor
    ⍝ cfg = Configuration instance
      PS←(⊃⊃⎕CLASS ⎕THIS).##
      :If 9.2=⎕NC⊂'cfg'
      :AndIf PS.API.Configuration≡⊃⊃⎕CLASS cfg
          _cfg←cfg
      :Else
          'Requires a Configuration instance'⎕SIGNAL 11
      :EndIf
      _exportedTime←3⊃⎕AI
      _bufferLength←0
      _isShuttingDown←0
     
      _metrics←⎕NS''
      _resourceMetric←_metrics.⎕NS''
      _metrics.resourceMetrics←,_resourceMetric
      resource←_resourceMetric.(resource←⎕NS'')
      resource.attributes←toKeyValue/2 2⍴'service.name'_cfg.ServiceName'service.version'_cfg.ServiceVersion
      _resourceMetric.scopeMetrics←⍬
     
      _httpClt←⎕NEW PS.HttpCommand
      _httpClt.Command←'post'
      _httpClt.URL←_cfg.OtelHttpUrlMetrics
      _httpClt.ContentType←'application/json'
    ∇

    ∇ r←GetMeter args;ind;scopeName;scopeVersion;attrs;scopeMetric
      :Implements Method IMeterProvider.GetMeter
      ⍝ args = ScopeName [ScopeVersion] [attributes]
      args←,⊆args
      args←args,(1=≢args)/⊂_cfg.ServiceVersion
      scopeName scopeVersion←2↑args
      attrs←toKeyValueMatrix 2↓args
      scopeMetric←_resourceMetric.⎕NS''
      scopeMetric.(scope←⎕NS'')
      scopeMetric.scope.(name version)←scopeName scopeVersion
      scopeMetric.scope.attributes←toKeyValue/attrs
      scopeMetric.metrics←⍬
      _resourceMetric.scopeMetrics,←scopeMetric
      r←⎕NEW PS.Meter(,scopeMetric)
    ∇

    ∇ {r}←Shutdown
      :Implements Method IMeterProvider.Shutdown
      _isShuttingDown←1
      r←ForceFlush
      _instruments←⍬
    ∇

    ∇ {r}←ForceFlush
      :Implements Method IMeterProvider.ForceFlush
      ExportMetrics 1
      r←0
    ∇

    ∇ instrument←CreateCounter(scopeMetric args)
      :Implements Method IMeterProvider.CreateCounter
      instrument←⎕NEW PS.Counter(scopeMetric args)
      _instruments,←instrument
    ∇

    ∇ instrument←CreateGauge(scopeMetric args)
      :Implements Method IMeterProvider.CreateGauge
      instrument←⎕NEW PS.Gauge(scopeMetric args)
      _instruments,←instrument
    ∇

    ∇ instrument←CreateHistogram(scopeMetric args)
      :Implements Method IMeterProvider.CreateHistogram
      instrument←⎕NEW PS.Histogram(scopeMetric args)
      _instruments,←instrument
    ∇

    ∇ ProcessMetrics
      :Access Public Instance
      _bufferLength+←1
      ExportMetrics 0
    ∇

    ∇ ExportMetrics flush;callspace;exporter;fn;rs;scopeMetric;scopeSpan
      :Access Public Instance
      ⍝ Export metrics for further processing such as batching, sampling and/or enrichment
      ⍝ flush = 1 to force flush buffered meters
      :If 0=≢_instruments
          :Return
      :EndIf
      :If flush∧0<_bufferLength
      :OrIf (~_isShuttingDown)∧(_cfg.BatchSize≤_bufferLength)∧(_cfg.ExportInterval≤(3⊃⎕AI)-_exportedTime)
          :Trap _cfg.Debug↓0
              _instruments.Collect
              :If 0=≢_cfg.Exporters
                  PostHttpJson _metrics
              :EndIf
              :For exporter :In _cfg.Exporters
                  callspace fn←exporter
                  (callspace⍎fn)_metrics
              :EndFor
              _instruments.Clear
              _exportedTime←3⊃⎕AI
              _bufferLength←0
          :EndTrap
      :EndIf
    ⍝
    ∇

    ∇ PostHttpJson metrics;res
      :Hold 'httpPostMeter'
          _httpClt.Params←metrics
          res←_httpClt.Run
      :EndHold
      LogError(200≠⊃res.HttpStatus)'Failed posting metric'
    ∇

    ∇ LogError msg
      :If _cfg.Debug∧0≠⊃msg
          ⎕←1↓msg
      :EndIf
    ∇

:EndClass
