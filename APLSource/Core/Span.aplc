:Class Span: ISpan
    :Include Tools

    :Field Private Instance name
    :Field Private Instance traceId
    :Field Private Instance parentSpanId
    :Field Private Instance spanId
    :Field Private Instance startTime
    :Field Private Instance endTime
    :Field Private Instance kind
    :Field Private Instance attributes

    :Field Private Instance events
   ⍝ An n-element array with events. Each element
   ⍝ contains an Event name and Event attribute.

    :Field Private Instance links
    :Field Private Instance isEnded
    :Field Private Instance statusCode
    :Field Private Instance statusMessage
    :Field Private Instance tid
    :Field Private Instance scopeSpan

    ∇ make context
      :Access Public
      :Implements Constructor
      PS←(⊃⊃⎕CLASS ⎕THIS).##
      :If {0::1 ⋄ ~PS.ITracerProvider∊⊃⎕CLASS ⍵}⊃⎕RSI
          isEnded←1
          'Spans must be created from tracer'⎕SIGNAL 982
      :EndIf
      isEnded←0
     
      (scopeSpan traceId parentSpanId spanId name)←context
     
      startTime←Now
      endTime←0
      kind←PS.API.SpanKind.INTERNAL
      attributes←0 2⍴⊂''
      events←⍬
      links←⍬
      statusCode←PS.API.SpanStatusCode.UNSET
      statusMessage←''
      tid←⎕TID
      ⍝
    ∇

    ∇ die
      :Access Private
      :Implements Destructor
      →isEnded/0
      End
    ∇

    :Property Simple Scope,SpanId,TraceId,Tid ⍝EndTime,Kind,Name,ParentSpanId,SpanId,StartTime,TraceId,Tid
    :Access Public
        ∇ r←get ipa
          r←('Scope' 'SpanId' 'TraceId' 'Tid'⍳⊂ipa.Name)⊃scopeSpan.scope spanId traceId tid
        ∇
    :EndProperty

    ∇ AddEvent args;attrm;r
      :Implements Method ISpan.AddEvent
      ⍝ Add a Span Event
      ⍝ args, vov: EventName [attributes]
      ⍝ attributes as Key/value pairs
      args←,⊆args
      attrm←toKeyValueMatrix 1↓args
      validateAttributes attrm
      r←⎕NS''
      r.name←⊃args
      r.timeUnixNano←timeUnixNano 0
      r.attributes←toKeyValue/attrm
      ⍝r.message←val
      events,←r
    ∇

    ∇ AddLink args;attrm;r;span
      :Implements Method ISpan.AddLink
      ⍝ Add a link to another Span
      ⍝ args, vov: Span [attributes]
      ⍝ attributes as Key/value pairs
      args←,⊆args
      span←⊃args
      r←⎕NS''
      r.traceId←span.TraceId
      r.spanId←span.SpanId
      r.traceState←''
      attrm←toKeyValueMatrix 1↓args
      validateAttributes attrm
      r.attributes←toKeyValue/attrm
      links,←r
      ⍝
    ∇


    ∇ r←GetHttpContext
      :Implements Method ISpan.GetHttpContext
     ⍝ Returns the context (HTTP header) associated with this Span.
      r←1 2⍴'traceparent'('00-',traceId,'-',spanId,'-01')
     ⍝r⍪←'tracecontext' ''
    ∇

    ∇ r←GetAttribute attr;lookup
      :Implements Method ISpan.GetAttribute
     ⍝ Get a single Attribute with the
     ⍝ key passed as argument.
     ⍝ attr, cv: attribute key
     ⍝ Return, Attribute value
      lookup←attributes[;1]⍳⊆attr
      :If lookup≤≢attributes
          r←⊃attributes[lookup;2]
      :Else
          LogError 6('Attribute [',attr,'] not found')
      :EndIf
    ∇

    ∇ SetAttributes attrs;attrm
      :Implements Method ISpan.SetAttributes
     ⍝ Sets Attributes to the Span
     ⍝ ra, nv: 2-elements key val pairs
      attrm←toKeyValueMatrix attrs
      validateAttributes attributes⍪attrm
      attributes⍪←attrm
    ∇

    ∇ SetKind ra
      :Access Public
      :If ~ra∊PS.API.SpanKind.(UNSPECIFIED INTERNAL SERVER CLIENT PRODUCER CONSUMER)
          'Invalid span kind'⎕SIGNAL 11
      :EndIf
      kind←ra
    ∇

    ∇ r←{msg}SetStatus status
      :Access Public Instance
   ⍝ Sets status of the Span
   ⍝ ra, ns: 0 - OK, 1 - Error
   ⍝ la, cv: optional status message
   ⍝ Return, Span
      :If ~status∊PS.API.SpanStatusCode.(OK ERROR)
          'Invalid status code'⎕SIGNAL 11
      :EndIf
      :If 2=⎕NC'msg'
          :If 0=10|⎕DR msg
              statusMessage←msg
          :Else
              'A status message must be a string'⎕SIGNAL 11
          :EndIf
      :EndIf
      statusCode←status
    ∇

    ∇ r←UpdateName _name
    ⍝ Update the Span name
    ⍝ rarg, name to update
    ⍝ Return, Span
      →isEnded/0
      name←_name
    ∇

    ∇ End;r
⍝   Marks the end of Span execution.
      :Implements Method ISpan.End
      →isEnded/0
      endTime←Now
      isEnded←1
     
      r←scopeSpan.⎕NS''
      r.name←name
      r.traceId←traceId
      r.parentSpanId←parentSpanId
      r.spanId←spanId
      r.startTimeUnixNano←timeUnixNano startTime
      r.endTimeUnixNano←timeUnixNano endTime
      r.kind←kind
      r.attributes←toKeyValue/attributes
      r.events←events
      r.links←links
      r.status←r.⎕NS''
      r.status.code←statusCode
      r.status.message←statusMessage
      scopeSpan.spans,←r
      ##.End ⎕THIS
    ∇

    ∇ LogError msg
      ##.LogError msg
    ∇

:EndClass
