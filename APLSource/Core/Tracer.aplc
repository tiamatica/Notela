:Class Tracer: ITracer
⍝Handles span creation and in-process context propagation.
⍝This class provides methods for manipulating the context,
⍝creating spans, and controlling spans’ lifecycles.

    :Field Private Instance _scopeSpan

    ∇ make1(scopeSpan)
      :Access Public
      :Implements Constructor
      _scopeSpan←scopeSpan
    ∇

    ∇ r←GetContext
      :Implements Method ITracer.GetContext
      r←##.GetContext
    ∇

    ∇ r←GetActiveSpan
      :Implements Method ITracer.GetActiveSpan
      r←##.GetActiveSpan
    ∇

    ∇ {r}←SetActiveSpan span
      :Implements Method ITracer.SetActiveSpan
      ⍝ Set span as active.
      ⍝ r: return code (0=success,1=failure)
      :If span.isEnded
        r←1
      :Else
        r←##.SetActiveSpan span
      :EndIF
    ∇

    ∇ span←{context}StartSpan name
      :Implements Method ITracer.StartSpan
     ⍝ Create span with name in context.
     ⍝ If no context provided, create span in active span.
     ⍝ If no active span, create new root span.
     ⍝ Does not set the span as active (consider using StartActiveSpan for that).
      :If 0=⎕NC'context'
          span←##.StartSpan _scopeSpan name
      :Else
          span←context ##.StartSpan _scopeSpan name
      :EndIf
    ∇

    ∇ span←{context}StartActiveSpan name;currentSpan;row;list
      :Implements Method ITracer.StartActiveSpan
     ⍝ Create span with name in active context.
     ⍝ If no active context, create new root span.
     ⍝ Sets the span as active.
      :If 0=⎕NC'context'
          span←##.StartActiveSpan _scopeSpan name
      :Else
          span←context ##.StartActiveSpan _scopeSpan name
      :EndIf
    ∇

:EndClass
